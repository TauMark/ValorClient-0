class
 refid "kabam.rotmg.market.ui:MarketCreateOfferScreen/OfferEntry"
 instance QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen"), "OfferEntry")
  extends QName(PackageNamespace("flash.display"), "Sprite")
  flag SEALED
  iinit
   name "OfferEntry"
   refid "kabam.rotmg.market.ui:MarketCreateOfferScreen/OfferEntry/instance/init"
   body
    maxstack 6
    localcount 1
    initscopedepth 0
    maxscopedepth 1
    code
     getlocal0
     pushscope

     getlocal0
     constructsuper      0

     getlocal0
     findpropstrict      QName(PackageNamespace("org.osflash.signals"), "Signal")
     getlex              QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen"), "OfferEntry")
     constructprop       QName(PackageNamespace("org.osflash.signals"), "Signal"), 1
     setproperty         QName(PackageNamespace(""), "removeOffer")

     pushbyte            10
     findproperty        QName(PackageNamespace(""), "x")
     swap
     setproperty         QName(PackageNamespace(""), "x")

     getlex              QName(PackageNamespace(""), "graphics")
     pushbyte            0
     pushbyte            1
     callpropvoid        QName(PackageNamespace(""), "beginFill"), 2

     getlex              QName(PackageNamespace(""), "graphics")
     pushbyte            0
     pushbyte            0
     pushshort           560
     pushbyte            120
     callpropvoid        QName(PackageNamespace(""), "drawRect"), 4

     getlex              QName(PackageNamespace(""), "graphics")
     callpropvoid        QName(PackageNamespace(""), "endFill"), 0

     getlex              QName(PackageNamespace(""), "graphics")
     pushbyte            1
     pushint             16777215
     pushbyte            1
     callpropvoid        QName(PackageNamespace(""), "lineStyle"), 3

     getlex              QName(PackageNamespace(""), "graphics")
     pushbyte            0
     pushbyte            0
     pushshort           560
     pushbyte            120
     callpropvoid        QName(PackageNamespace(""), "drawRect"), 4

     getlex              QName(PackageNamespace(""), "graphics")
     callpropvoid        QName(PackageNamespace(""), "lineStyle"), 0

     getlocal0
     findpropstrict      QName(PackageNamespace("kabam.rotmg.market.ui"), "MarketInventorySlot")
     constructprop       QName(PackageNamespace("kabam.rotmg.market.ui"), "MarketInventorySlot"), 0
     setproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "sellSlot")

     getlocal0
     getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "sellSlot")
     pushbyte            5
     setproperty         QName(PackageNamespace(""), "x")

     getlocal0
     getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "sellSlot")
     pushbyte            10
     setproperty         QName(PackageNamespace(""), "y")

     findpropstrict      QName(PackageNamespace(""), "addChild")
     getlocal0
     getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "sellSlot")
     callpropvoid        QName(PackageNamespace(""), "addChild"), 1

     getlocal0
     findpropstrict      QName(PackageNamespace("com.company.assembleegameclient.account.ui"), "TextInputField")
     pushstring          "Offer Price"
     pushfalse
     constructprop       QName(PackageNamespace("com.company.assembleegameclient.account.ui"), "TextInputField"), 2
     setproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "priceInput")

     getlocal0
     getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "priceInput")
     pushbyte            55
     getlocal0
     getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "sellSlot")
     getproperty         QName(PackageNamespace(""), "width")
     add
     setproperty         QName(PackageNamespace(""), "x")

     getlocal0
     getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "priceInput")
     pushbyte            15
     setproperty         QName(PackageNamespace(""), "y")

     getlocal0
     getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "priceInput")
     getproperty         QName(PackageNamespace(""), "inputText_")
     pushstring          "0-9"
     setproperty         QName(PackageNamespace(""), "restrict")

     findpropstrict      QName(PackageNamespace(""), "addChild")
     getlocal0
     getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "priceInput")
     callpropvoid        QName(PackageNamespace(""), "addChild"), 1

     getlocal0
     findpropstrict      QName(PackageNamespace("com.company.assembleegameclient.ui"), "DeprecatedTextButton")
     pushbyte            21
     pushstring          "Remove"
     pushbyte            0
     pushtrue
     constructprop       QName(PackageNamespace("com.company.assembleegameclient.ui"), "DeprecatedTextButton"), 4
     setproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "removeButton")

     getlocal0
     getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "removeButton")
     getproperty         QName(PackageNamespace(""), "textChanged")
     getlocal0
     getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "alignUI")
     callpropvoid        QName(PackageNamespace(""), "add"), 1

     getlocal0
     getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "removeButton")
     pushstring          "click"
     getlocal0
     getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "onRemoveClick")
     callpropvoid        QName(PackageNamespace(""), "addEventListener"), 2

     findpropstrict      QName(PackageNamespace(""), "addChild")
     getlocal0
     getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "removeButton")
     callpropvoid        QName(PackageNamespace(""), "addChild"), 1

     returnvoid
    end ; code
   end ; body
  end ; method
  trait slot QName(PackageNamespace(""), "removeOffer") type QName(PackageNamespace("org.osflash.signals"), "Signal") end
  trait slot QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "priceInput") type QName(PackageNamespace("com.company.assembleegameclient.account.ui"), "TextInputField") end
  trait slot QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "removeButton") type QName(PackageNamespace("com.company.assembleegameclient.ui"), "DeprecatedTextButton") end
  trait slot QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "sellSlot") type QName(PackageNamespace("kabam.rotmg.market.ui"), "MarketInventorySlot") end
  trait method QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "alignUI")
   method
    name "alignUI"
    refid "kabam.rotmg.market.ui:MarketCreateOfferScreen/OfferEntry/instance/kabam.rotmg.market.ui:MarketCreateOfferScreen/OfferEntry/instance/alignUI"
    returns QName(PackageNamespace(""), "void")
    body
     maxstack 4
     localcount 1
     initscopedepth 0
     maxscopedepth 1
     code
      getlocal0
      pushscope

      getlocal0
      getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "removeButton")
      pushshort           560
      getlocal0
      getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "removeButton")
      getproperty         QName(PackageNamespace(""), "width")
      subtract
      pushbyte            20
      subtract
      setproperty         QName(PackageNamespace(""), "x")

      getlocal0
      getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "removeButton")
      pushbyte            120
      pushbyte            2
      divide
      getlocal0
      getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "removeButton")
      getproperty         QName(PackageNamespace(""), "height")
      pushbyte            2
      divide
      subtract
      setproperty         QName(PackageNamespace(""), "y")

      returnvoid
     end ; code
    end ; body
   end ; method
  end ; trait
  trait method QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "onRemoveClick")
   method
    name "onRemoveClick"
    refid "kabam.rotmg.market.ui:MarketCreateOfferScreen/OfferEntry/instance/kabam.rotmg.market.ui:MarketCreateOfferScreen/OfferEntry/instance/onRemoveClick"
    param QName(PackageNamespace("flash.events"), "MouseEvent")
    returns QName(PackageNamespace(""), "void")
    flag NEED_ACTIVATION
    flag HAS_PARAM_NAMES
    paramname "param1"
    body
     maxstack 6
     localcount 3
     initscopedepth 0
     maxscopedepth 2
     code
      getlocal0
      pushscope

      newactivation
      dup
      setlocal2

      pushscope

      findpropstrict      QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen"), "param1")
      getlocal1
      setproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen"), "param1")

      pushnull
      coerce              QName(PackageNamespace("kabam.rotmg.pets.view.dialogs"), "PetDialog")
      getlocal2
      swap
      setslot             2

      getlex              QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen"), "param1")
      coerce              QName(PackageNamespace("flash.events"), "MouseEvent")
      getlocal2
      swap
      setslot             3

      findpropstrict      QName(PackageNamespace("kabam.rotmg.pets.view.dialogs"), "PetDialog")
      pushstring          "Confirm"
      pushstring          "Are you sure you want to remove this offer from your creation list?"
      pushstring          "Remove"
      pushstring          "Keep it"
      pushstring          "/removeConfirm"
      constructprop       QName(PackageNamespace("kabam.rotmg.pets.view.dialogs"), "PetDialog"), 5
      coerce              QName(PackageNamespace("kabam.rotmg.pets.view.dialogs"), "PetDialog")
      getlocal2
      swap
      setslot             2

      getlex              QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen"), "dialog")
      callpropvoid        QName(PackageNamespace(""), "addFullDim"), 0

      getlex              QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen"), "dialog")
      pushstring          "dialogRightButton"
      newfunction         "kabam.rotmg.market.ui:MarketCreateOfferScreen/OfferEntry/instance/kabam.rotmg.market.ui:MarketCreateOfferScreen/OfferEntry/instance/onRemoveClick/inline_method"
      callpropvoid        QName(PackageNamespace(""), "addEventListener"), 2

      getlex              QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen"), "dialog")
      pushstring          "dialogLeftButton"
      getlocal0
      getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "onRemoveOffer")
      callpropvoid        QName(PackageNamespace(""), "addEventListener"), 2

      getlex              QName(PackageNamespace(""), "parent")
      getproperty         QName(PackageNamespace(""), "parent")
      getproperty         QName(PackageNamespace(""), "parent")
      getlex              QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen"), "dialog")
      callpropvoid        QName(PackageNamespace(""), "addChild"), 1

      returnvoid
     end ; code
     trait slot QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen"), "param1") slotid 1 type QName(PackageNamespace("flash.events"), "MouseEvent") end
     trait slot QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen"), "dialog") slotid 2 type QName(PackageNamespace("kabam.rotmg.pets.view.dialogs"), "PetDialog") end
     trait slot QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen"), "event") slotid 3 type QName(PackageNamespace("flash.events"), "MouseEvent") end
    end ; body
   end ; method
  end ; trait
  trait method QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "onRemoveOffer")
   method
    name "onRemoveOffer"
    refid "kabam.rotmg.market.ui:MarketCreateOfferScreen/OfferEntry/instance/kabam.rotmg.market.ui:MarketCreateOfferScreen/OfferEntry/instance/onRemoveOffer"
    param QName(PackageNamespace("flash.events"), "Event")
    returns QName(PackageNamespace(""), "void")
    flag HAS_PARAM_NAMES
    paramname "param1"
    body
     maxstack 3
     localcount 2
     initscopedepth 0
     maxscopedepth 1
     code
      getlocal0
      pushscope

      getlocal1
      getproperty         QName(PackageNamespace(""), "target")
      getlex              QName(PackageNamespace("kabam.rotmg.pets.view.dialogs"), "PetDialog")
      astypelate
      getproperty         QName(PackageNamespace(""), "parent")
      getlocal1
      getproperty         QName(PackageNamespace(""), "target")
      getlex              QName(PackageNamespace("kabam.rotmg.pets.view.dialogs"), "PetDialog")
      astypelate
      callpropvoid        QName(PackageNamespace(""), "removeChild"), 1

      getlocal0
      getproperty         QName(PackageNamespace(""), "removeOffer")
      getlocal0
      callpropvoid        QName(PackageNamespace(""), "dispatch"), 1

      returnvoid
     end ; code
    end ; body
   end ; method
  end ; trait
  trait method QName(PackageNamespace(""), "isValidOffer")
   method
    name "isValidOffer"
    refid "kabam.rotmg.market.ui:MarketCreateOfferScreen/OfferEntry/instance/isValidOffer"
    returns QName(PackageNamespace(""), "Boolean")
    body
     maxstack 2
     localcount 2
     initscopedepth 0
     maxscopedepth 1
     code
      getlocal0
      pushscope

      getlocal0
      getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "priceInput")
      callproperty        QName(PackageNamespace(""), "text"), 0
      convert_i
      setlocal1

      getlocal1
      pushbyte            0
      lessthan
      dup
      iftrue              L17

      pop
      getlocal0
      getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "priceInput")
      callproperty        QName(PackageNamespace(""), "text"), 0
      not
L17:
      iffalse             L24

      getlocal0
      getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "priceInput")
      pushstring          "Invalid Price"
      callpropvoid        QName(PackageNamespace(""), "setError"), 1

      pushfalse
      returnvalue

L24:
      getlocal0
      getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "sellSlot")
      getproperty         QName(PackageNamespace(""), "itemData")
      getproperty         QName(PackageNamespace(""), "objectType")
      pushbyte            -1
      equals
      not
      returnvalue
     end ; code
    end ; body
   end ; method
  end ; trait
  trait method QName(PackageNamespace(""), "makeOffer")
   method
    name "makeOffer"
    refid "kabam.rotmg.market.ui:MarketCreateOfferScreen/OfferEntry/instance/makeOffer"
    returns QName(PackageNamespace("kabam.rotmg.messaging.impl.data"), "MarketOffer")
    body
     maxstack 2
     localcount 2
     initscopedepth 0
     maxscopedepth 1
     code
      getlocal0
      pushscope

      findpropstrict      QName(PackageNamespace("kabam.rotmg.messaging.impl.data"), "MarketOffer")
      constructprop       QName(PackageNamespace("kabam.rotmg.messaging.impl.data"), "MarketOffer"), 0
      coerce              QName(PackageNamespace("kabam.rotmg.messaging.impl.data"), "MarketOffer")
      setlocal1

      getlocal1
      getlocal0
      getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "sellSlot")
      callproperty        QName(PackageNamespace(""), "makeObjectSlot"), 0
      setproperty         QName(PackageNamespace(""), "objectSlot")

      getlocal1
      getlocal0
      getproperty         QName(PrivateNamespace("FilePrivateNS:MarketCreateOfferScreen:OfferEntry"), "priceInput")
      callproperty        QName(PackageNamespace(""), "text"), 0
      convert_i
      setproperty         QName(PackageNamespace(""), "price")

      getlocal1
      returnvalue
     end ; code
    end ; body
   end ; method
  end ; trait
 end ; instance
 cinit
  refid "kabam.rotmg.market.ui:MarketCreateOfferScreen/OfferEntry/class/init"
  body
   maxstack 0
   localcount 1
   initscopedepth 0
   maxscopedepth 0
   code
    returnvoid
   end ; code
  end ; body
 end ; method
 trait const QName(PackageNamespace(""), "WIDTH") type QName(PackageNamespace(""), "int") value Integer(560) end
 trait const QName(PackageNamespace(""), "HEIGHT") type QName(PackageNamespace(""), "int") value Integer(120) end
end ; class
